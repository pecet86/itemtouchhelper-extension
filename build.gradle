// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.1'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

plugins {
    id "com.jfrog.bintray" version "1.7"
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        google()
        maven { url "https://jitpack.io" }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply plugin: 'org.ajoberstar.grgit'
def versionName = '2.0.0'
def tagName = "v" + versionName

def getStatus() {
    def describe = grgit.describe()
    def pattern = /^(.*)-([0-9]+)-(g[0-9a-z]+)$/
    def tag = describe =~ pattern

    if (tag.matches()) {
        tag = tag[0][1];
        def logs = grgit.log {
            range(tag, 'HEAD')
        }
        if (!logs.isEmpty()) {
            return logs
                    .collect { "Autor: ${it.author.name}; Data: ${it.getDate().format('yyyy-MM-dd HH:mm:ss')}; Opis:${it.fullMessage}" }
                    .asImmutable()
                    .toString()
        } else {
            return null
        }
    } else {
        return null
    }
}

task releng {
    description = "Application release: $tagName"
    doLast {
        def changes = getStatus()
        if (changes == null) {
            throw new RuntimeException("There are changes not commited yet.\n$changes")
        }

        //add tag
        grgit.tag.add {
            name = tagName
            message = "Release of $tagName"
        }
    }
}

ext {
    configs = [
            compileSdkVersion: 30,
            buildToolsVersion: '30.0.2',
            minSdkVersion    : 26,
            targetSdkVersion : 30,
            versionCode      : 2,
            versionName      : versionName,
    ]

    libs = [
            appcompat: '1.2.0',
            design   : '1.2.1',
    ]
}